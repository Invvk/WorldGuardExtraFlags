plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'kr.entree.spigradle' version '2.4.2'
    id "net.kyori.indra.git" version "2.1.1"
}

group 'io.github.invvk'
version "$parent.version"

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly 'net.essentialsx:EssentialsX:2.19.4'
    compileOnly 'me.clip:placeholderapi:2.11.1'

    implementation "org.bstats:bstats-bukkit:3.0.0"
    implementation project(":WGEF-Abstraction")
    implementation project(":WG7")
}

shadowJar {
    archiveBaseName.set('WGEF-REBORN')
    archiveClassifier.set('')
    archiveVersion.set('')

    destinationDirectory = file(base118)

    exclude 'META-INF/**'
    exclude 'LICENSE'

    relocate 'org.bstats', 'io.github.invvk.wgef.metrics'

    finalizedBy('clean', ':WG7:clean', ':WGEF-Abstraction:clean')
}

spigot {
    main 'io.github.invvk.wgef.WGEFPlugin'
    name 'WGEF-Reborn'
    authors 'Invvk', 'isokissa3'
    apiVersion '1.17'
    version "${parent.version}+${indraGit.commit().abbreviate(7).name()}"
    depends 'WorldGuard'
    softDepends 'FastAsyncWorldEdit', 'Essentials', 'TAB'
    description 'Adds more flags to WorldGuard to help manage your server easily!'
}